plugins {
    id "java"
    id "org.openmicroscopy.insight"
    id "org.openmicroscopy.publish"
    id "org.openmicroscopy.distribute"
    id "org.openmicroscopy.packager"
    id "org.openmicroscopy.additional-repositories" version "5.7.2"
}

group = "org.openmicroscopy"
version = "5.8.7-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
}

//java {
//    sourceCompatibility = JavaVersion.VERSION_11
//    targetCompatibility = JavaVersion.VERSION_11
//}

dependencies {
    testImplementation("junit:junit:4.12")

    if (JavaVersion.current().isJava9Compatible()) {
        implementation('com.sun.activation:javax.activation:1.2.0')
    }
    implementation("com.mortennobel:java-image-scaling:0.8.6")
    implementation("com.google.code.gson:gson:2.8.5")
    implementation("com.zeroc:glacier2:3.6.5")
    implementation("info.clearthought:table-layout:4.3.0")
    implementation("insight:JHotDraw:7.0.9")
    implementation("net.imagej:ij:1.48s")
    implementation("net.java.dev.jna:jna-platform:4.5.2") {
        exclude group: "com.sun.jna", module: "jna"
    }
    implementation("org.apache.poi:poi:4.0.1")
    implementation("org.apache.commons:commons-collections4:4.3")
    implementation("org.apache.httpcomponents:httpmime:4.5.7")
    implementation("org.apache.httpcomponents:httpclient:4.5.7")
    implementation("org.apache.httpcomponents:httpcomponents-client:4.5.7")
    implementation("org.jfree:jfreechart:1.0.19")
    implementation("org.swinglabs:swingx:1.6.1")

    implementation("org.openmicroscopy:omero-gateway:5.9.3") {
        // Conflicts with `net.java.dev.jna`
        exclude group: "com.sun.jna", module: "jna"
    }
    implementation("ch.qos.logback:logback-classic:1.3.14")
    implementation("ch.qos.logback:logback-core:1.3.14")
}

test {
    testLogging.showStandardStreams = true
    systemProperties = System.getProperties()
    systemProperties["omero.user"] = "user"
    systemProperties["omero.pass"] = "changeMe"
    systemProperties["omero.host"] = "localhost"
    systemProperties["omero.port"] = "4064"
    systemProperties["omero.portSSL"] = "4063"
    filter {
        // Exclude the tests related to 239dc7fe9b000e6ceb0457b30a87569ec0619cfc
        // When we revert the above commit, the exclusion should be removed
        excludeTestsMatching "TestExecCommandStateTrans.testCancelWhenCancelled"
        excludeTestsMatching "TestExecCommandStateTrans.testRunWhenCancelled"
        excludeTestsMatching "TestExecCommandStateTrans.testExecutingCancelled"
    }
}
